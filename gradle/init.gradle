/**
 * A Gradle plugin to generate a JSON file with build information.
 *
 * Reference:
 * - https://github.com/nwillc/buildInfo/blob/master/src/main/groovy/com/github/nwillc/buildInfo/BuildInfoPlugin.groovy
 */

import java.nio.file.Paths
import groovy.json.JsonOutput
import org.gradle.api.Plugin
import org.gradle.api.Project

class GradleBuildManifest implements Plugin<Project> {
    private static final String[] SYSTEM_PROPERTIES = [
        "java.vendor",
        "java.version",
    ]

    @Override
    void apply(Project project) {
        project.extensions.create("buildManifest", GradleBuildManifest.Extension)

        project.task('build-manifest') {
            doLast {
                def projectVersion = project.version == "unspecified" ? "" : project.version
                
                def manifest = [
                    // Gradle
                    "GRADLE_VERSION": project.gradle.gradleVersion,
                    // Project
                    "PROJECT_NAME": project.name,
                    "PROJECT_DESCRIPTION": project.description ? project.description : "",
                    "PROJECT_GROUP": project.group,
                    "PROJECT_VERSION": projectVersion,
                    "PROJECT_PROFILE": "",
                    // Build
                    "BUILD_ARTIFACT_ID": "${project.name}-${projectVersion}",
                    "BUILD_ARTIFACT": "${project.name}-${projectVersion}.jar",
                    // Compatibility
                    "SOURCE_COMPATIBILITY": "",
                    "TARGET_COMPATIBILITY": ""
                ]

                if (project.hasProperty('profile')) {
                    manifest["PROJECT_PROFILE"] = project.profile
                }

                if (project.hasProperty('sourceCompatibility')) {
                    manifest["SOURCE_COMPATIBILITY"] = project.sourceCompatibility.toString()
                }
                if (project.hasProperty('targetCompatibility')) {
                    manifest["TARGET_COMPATIBILITY"] = project.targetCompatibility.toString()
                }

                SYSTEM_PROPERTIES.each { 
                    def key = it.replace('.', '_').toUpperCase()
                    manifest[key] = System.getProperty(it)
                }

                println "Generating " + project.buildManifest.output
                new File(project.buildManifest.output).write JsonOutput.prettyPrint(JsonOutput.toJson(manifest))
            }
        }
    }

    static class Extension {
        String output = Paths.get("build-manifest.json").toString()
    }
}

class GradleBuildManifestPlugin implements Plugin<Gradle> {
    void apply(Gradle gradle) {
        gradle.rootProject { project -> 
            project.apply plugin: GradleBuildManifest
        }
    }
}

apply plugin: GradleBuildManifestPlugin
